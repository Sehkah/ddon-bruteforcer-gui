plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin'
    id 'org.beryx.jlink'
    id 'org.javamodularity.moduleplugin'
}

ext {
    netJavaDevJnaVersion = '5.10.0'
    commonsIoVersion = '2.11.0'
    orgApacheLoggingLog4jVersion = '2.17.2'
    comFasterxmlJacksonVersion = '2.13.2'
    orgKordampIkonliVersion = '12.3.1'
    orgBouncycastleVersion = '1.70'
    junitVersion = '5.8.2'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8', '-Djna.library.path=lib/win32-x86-64']
    mainModule = 'org.sehkah.ddon.tools'
    mainClass = 'org.sehkah.ddon.tools.Main'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    implementation group: 'net.java.dev.jna', name: 'jna', version: "${netJavaDevJnaVersion}"
    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: "${commonsIoVersion}"
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-api
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${orgApacheLoggingLog4jVersion}"
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${orgApacheLoggingLog4jVersion}"
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "${comFasterxmlJacksonVersion}"
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${comFasterxmlJacksonVersion}"
    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-javafx
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-javafx', version: "${orgKordampIkonliVersion}"
    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-bootstrapicons-pack
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-bootstrapicons-pack', version: "${orgKordampIkonliVersion}"
    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: "${orgBouncycastleVersion}"

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitVersion}"
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-params
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${junitVersion}"
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
}

test {
    useJUnitPlatform()

    systemProperty('jna.library.path', 'lib/win32-x86-64')
}

jar {
    manifest {
        attributes 'Main-Class': 'org.sehkah.ddon.tools.Main'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--ignore-signing-information']
//    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    launcher {
        name = rootProject.name
        jvmArgs = ['-Dfile.encoding=UTF-8', '-Djna.library.path=lib/win32-x86-64']
    }

    jpackage {
        jvmArgs = ['-Dfile.encoding=UTF-8', '-Djna.library.path=lib/win32-x86-64']
        skipInstaller = true
        imageOptions += ['--win-console']
        installerOptions += [
                '--vendor', 'Sehkah',
                '--app-version', version
        ]
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser', '--win-menu'
            ]
        }
    }
    forceMerge('jackson', 'log4j')
    mergedModule {
        additive = true
        uses 'com.fasterxml.jackson.databind.Module'
    }
}

jpackage.doLast {
    copy {
        from 'lib/win32-x86-64/'
        into "$buildDir/jpackage/$rootProject.name/lib/win32-x86-64"
    }
}

//
//jlinkZip {
//    group = 'distribution'
//    doLast {
//        copy {
//            from "lib/win32-x86-64/SeededXorshift128.dll"
//            into "$imageDir/lib/win32-x86-64"
//        }
//    }
//}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(sourceCompatibility.toString())
        vendor = JvmVendorSpec.matching('Eclipse')
    }
}